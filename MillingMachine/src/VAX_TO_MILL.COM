$ ! EYEMILL.COM
$ ! Command file to send milling machine control programs,
$ ! which have been generated by the EYE proton therapy planning program, 
$ ! to the Cyclotron n.c. milling machine via a designated port.
$ !
$ ! Requires one parameter - the output port
$ !
$ IF P1 .NES. "" THEN GOTO OK
$ WRITE SYS$OUTPUT " "
$ INQUIRE P1 "Which output port to use (return ==> FRAISE ) ?"
$ IF P1 .EQ. "" THEN GOTO FRAISE
$OK:
$ P1 = F$EDIT(P1,"UPCASE")
$ IF F$LOCATE(":",P1) .EQ. F$LEN(P1) THEN P1 = P1 + ":"
$ GOTO SUITE
$ !
$ ! Select and set up output port
$ ! SET TERM/SPEED=2400/PARITY=EVEN/NOEIGHT/PERM 'P1'
$FRAISE:
$ P1 = "FRAISE"
$SUITE:
$ IF .NOT. $STATUS THEN EXIT
$ WRITE SYS$OUTPUT " "
$ WRITE SYS$OUTPUT "Files will be sent to port ''P1'"
$ WRITE SYS$OUTPUT " "
$ WRITE SYS$OUTPUT "Set milling machine to 'READ-IN ALL PROGRAMS'"
$ SET NOON
$ ETX[0,8] = 3
$ !
$ ! Fetch file names and output them
$LOOP:
$ ! Default directory
$ !
$ !
$ WRITE SYS$OUTPUT " "
$ WRITE SYS$OUTPUT " "
$ WRITE SYS$OUTPUT "Tape le chiffre qui correspond au directory"
$ WRITE SYS$OUTPUT " "
$ WRITE SYS$OUTPUT "       Modulateur          =  1"
$ WRITE SYS$OUTPUT "       Collimateur         =  2"
$ WRITE SYS$OUTPUT "       Programmes          =  3"
$ WRITE SYS$OUTPUT "       Directory medical   =  4"
$ WRITE SYS$OUTPUT "       Divers              =  5"
$ WRITE SYS$OUTPUT " "
$ INQUIRE MILL_DIR "       Choix (return to end) "
$ WRITE SYS$OUTPUT " "
$ WRITE SYS$OUTPUT " "
$ IF MILL_DIR .EQ. "" THEN GOTO FINISH
$ IF MILL_DIR .EQ. 1 THEN -
    DEF_DIR = "[CHARLY.MILL.MODU]"
$ IF MILL_DIR .EQ. 2 THEN -
    DEF_DIR = "[CHARLY.MILL.COLLI]"
$ IF MILL_DIR .EQ. 3 THEN -
    DEF_DIR = "[CHARLY.MILL.PROGRAM]"
$ IF MILL_DIR .EQ. 4 THEN -
    DEF_DIR = "[EYEPLN.PLAN]"
$ IF MILL_DIR .EQ. 5 THEN -
    DEF_DIR = "[CHARLY.MILL.DIVERS]"
$ DIR/PAGE 'DEF_DIR'*.MIL
$ WRITE SYS$OUTPUT " "
$ INQUIRE MILL_FILE "Name of milling file, or <return> to end"
$ IF MILL_FILE .EQS. "" THEN GOTO FINISH
$ MILL_FILE = F$EDIT(MILL_FILE,"UPCASE")
$ ! Default file extension to .MIL
$ FILE_NAME = MILL_FILE
$ LEN = F$LEN(FILE_NAME)
$ END_DIR = F$LOCATE("]",FILE_NAME)
$ IF END_DIR .NE. LEN THEN -
    FILE_NAME = F$EXTRACT(END_DIR,LEN-END_DIR,FILE_NAME)
$ IF F$LOCATE(".",FILE_NAME) .EQ. F$LEN(FILE_NAME) THEN -
    MILL_FILE = MILL_FILE + ".MIL"
$ ! Default directory to DEF_DIR
$ IF F$LOCATE("[",MILL_FILE) .EQ. F$LEN(MILL_FILE) THEN -
    MILL_FILE = DEF_DIR + MILL_FILE
$ COPY 'MILL_FILE' 'P1'
$ IF $STATUS THEN -
    WRITE SYS$OUTPUT "File ''MILL_FILE' output to port ''P1'"
$ GOTO LOOP
$ !
$ ! All done - write final ETX & restore output port
$FINISH:
$ OPEN/WRITE MILLTERM 'P1'
$ WRITE MILLTERM "''ETX'"
$ CLOSE MILLTERM
